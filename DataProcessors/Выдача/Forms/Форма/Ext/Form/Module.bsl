
&НаСервереБезКонтекста
Функция ВыдатьЗаказы(Знач МассивЗаказов)
	
	Ссылка = Документы.Выдача.ВыдатьЗаказы(МассивЗаказов);
	
	Возврат ЗначениеЗаполнено(Ссылка);
	
КонецФункции

&НаКлиенте
Процедура Выдать(Команда)
	
	ПриемкаУспешна = ВыдатьЗаказы(ВыбранныеЗаказы.ВыгрузитьЗначения());
	
	Если ПриемкаУспешна Тогда
		ВыбранныеЗаказы.Очистить();
		УстановитьЗаголовокКнопкиВыдать();
		Элементы.ЗаказыКВыдаче.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗаголовокКнопкиВыдать()
	
	Результат = "Выдать";
	Если ВыбранныеЗаказы.Количество() <> 0 Тогда
		Результат = СтрШаблон("Выдать [%1]", Формат(ВыбранныеЗаказы.Количество(), "ЧГ="));
	КонецЕсли;
	
	Элементы.ФормаВыдать.Заголовок = Результат;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьЗаголовокКнопкиВыдать();
	
КонецПроцедуры


&НаСервере
Процедура УстановитьУсловноеОформлениеФормы()
	
	УсловноеОформление.Элементы.Очистить();
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = ЭлементОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЗаказыКВыдаче.Имя);
	
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЗаказыКВыдаче.ЗаказВыбран");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	Шрифт = Элементы.ЗаказыКВыдаче.Шрифт;
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(Шрифт,,,Истина));
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформлениеФормы();
	
	ЗаказыКВыдаче.Параметры.УстановитьЗначениеПараметра("ВыбранныеЗаказы", ВыбранныеЗаказы);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаказыКВыдачеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ДанныеСтроки = Элементы.ЗаказыКВыдаче.ДанныеСтроки(ВыбраннаяСтрока);
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ЗавершениеВыбораЗаказа",
		ЭтотОбъект,
		Новый Структура("Заказ", ДанныеСтроки.Заказ));
	
	Если Не ДанныеСтроки.ЗаказОплачен Тогда
		
		ПоказатьПредупреждение(, 
			НСтр("ru = 'Заказ не оплачен! Нужно зарегистрировать оплату'"));
		
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеВыбораЗаказа(Результат, ДополнительныеПараметры) Экспорт
	
	НайденныйЭлемент = ВыбранныеЗаказы.НайтиПоЗначению(ДополнительныеПараметры.Заказ);
	Если НайденныйЭлемент = Неопределено Тогда
		ВыбранныеЗаказы.Добавить(ДополнительныеПараметры.Заказ);
	Иначе
		ВыбранныеЗаказы.Удалить(НайденныйЭлемент);
	КонецЕсли;	
	
	ЗаказыКВыдаче.Параметры.УстановитьЗначениеПараметра("ВыбранныеЗаказы", ВыбранныеЗаказы.ВыгрузитьЗначения());
	
	УстановитьЗаголовокКнопкиВыдать();	
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПолученаОплатаНаСервере(Знач Заказ)

	Документы.ОплатаЗаказа.Зарегистрировать(Заказ);
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьПолучениеОплаты(Команда)
	
	ДанныеСтроки = Элементы.ЗаказыКВыдаче.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если ДанныеСтроки.ЗаказОплачен Тогда
		Возврат;
	КонецЕсли;
	
	ЗадатьВопросОбОплате(ДанныеСтроки.Заказ, ДанныеСтроки.Сумма);
	
КонецПроцедуры
	
&НаКлиенте
Процедура ЗадатьВопросОбОплате(Знач Заказ, Знач Сумма, Оповещение = Неопределено) 
	
	ДополнительныеПараметры = Новый Структура("Заказ", Заказ);
	Если Оповещение <> Неопределено Тогда
		ДополнительныеПараметры.Вставить("Оповещение", Оповещение);
	КонецЕсли;	
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПолученаОплата", ЭтотОбъект, ДополнительныеПараметры);
	
	ПоказатьВопрос(
		ОписаниеОповещения,
		НСтр(СтрШаблон("ru = 'Получатель предъявил чек из терминала оплаты на сумму %1 руб'", Сумма)),
		РежимДиалогаВопрос.ДаНетОтмена,,
		КодВозвратаДиалога.Нет,
		"Подтверждение оплаты");		
	
КонецПроцедуры

&НаКлиенте
Процедура ПолученаОплата(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ПолученаОплатаНаСервере(ДополнительныеПараметры["Заказ"]);
	
	Если ДополнительныеПараметры.Свойство("Оповещение") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, РезультатВопроса);
	КонецЕсли;
	
	Элементы.ЗаказыКВыдаче.Обновить();
КонецПроцедуры
