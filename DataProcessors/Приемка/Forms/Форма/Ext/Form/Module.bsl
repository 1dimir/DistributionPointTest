

&НаКлиенте
Процедура ЗаказыКПоступлениюВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ДанныеСтроки = Элементы.ЗаказыКПоступлению.ДанныеСтроки(ВыбраннаяСтрока);
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НайденныйЭлемент = ВыбранныеЗаказы.НайтиПоЗначению(ДанныеСтроки.Заказ);
	Если НайденныйЭлемент = Неопределено Тогда
		ВыбранныеЗаказы.Добавить(ДанныеСтроки.Заказ);
	Иначе
		ВыбранныеЗаказы.Удалить(НайденныйЭлемент);
	КонецЕсли;	
	
	ЗаказыКПоступлению.Параметры.УстановитьЗначениеПараметра("ВыбранныеЗаказы", ВыбранныеЗаказы.ВыгрузитьЗначения());
	
	УстановитьЗаголовокКнопкиПринять();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПринятьЗаказы(Знач МассивЗаказов)
	
	Ссылка = Документы.Приемка.ПринятьЗаказы(МассивЗаказов);
	
	Возврат ЗначениеЗаполнено(Ссылка);
	
КонецФункции

&НаКлиенте
Процедура Принять(Команда)
	
	ПриемкаУспешна = ПринятьЗаказы(ВыбранныеЗаказы.ВыгрузитьЗначения());
	
	Если ПриемкаУспешна Тогда
		ВыбранныеЗаказы.Очистить();
		УстановитьЗаголовокКнопкиПринять();
		Элементы.ЗаказыКПоступлению.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗаголовокКнопкиПринять()
	
	Результат = "Принять";
	Если ВыбранныеЗаказы.Количество() <> 0 Тогда
		Результат = СтрШаблон("Принять [%1]", Формат(ВыбранныеЗаказы.Количество(), "ЧГ="));
	КонецЕсли;
	
	Элементы.ФормаПринять.Заголовок = Результат;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьЗаголовокКнопкиПринять();
	
КонецПроцедуры


&НаСервере
Процедура УстановитьУсловноеОформлениеФормы()
	
	УсловноеОформление.Элементы.Очистить();
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = ЭлементОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЗаказыКПоступлению.Имя);
	
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЗаказыКПоступлению.ЗаказВыбран");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	Шрифт = Элементы.ЗаказыКПоступлению.Шрифт;
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(Шрифт,,,Истина));
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформлениеФормы();
	
	ЗаказыКПоступлению.Параметры.УстановитьЗначениеПараметра("ВыбранныеЗаказы", ВыбранныеЗаказы);
	
КонецПроцедуры
